TITLE:: Butz
summary:: Dynamic list of buttons with actions
categories:: Utilities
related:: Classes/NamedList

DESCRIPTION::

Butz has a named list of actions, which it shows on a GUI as buttons.
This is useful for opening multiple guis only when needed,
such as several instruments and their GUIs,
opening utilities like meters, custom scopes, mixers,
guis for tests for setup parts (speakers), etc etc.

It shows the actions as buttons with given style,
and updates dynamically when items are added.

Butz is a class-only singleton by design choice.

First code examples:

code::
Butz.show; // show the window for Butz

// add some instrument names in the order they should appear in
Butz.add(\manta);
Butz.add(\AI_NUFO);
Butz.add(\polyharpye);
Butz.add(\blippoo);
Butz.add(\theremax);
Butz.add('-');

// add some utility names
Butz.add(\tdefall);
Butz.add(\ndefmix);

// some names already with actions
Butz.add(\scope, { s.scope(4) });
Butz.add(\meter, { s.meter(6, 6).window.moveTo(0, 0) });
Butz.add(\testSnds); // add some names in order
Butz.add(\MUTE, { |bt| s.mute });
Butz.add(\UNMUTE, { |bt| s.unmute });

Butz.remove(\s_meter);

// add the actions later by name
Butz.add(\tdefall, { TdefAllGui(16).moveTo(10, 800) });

/// change the number of buttons and style aspectss
Butz.style;
Butz.numButz = 20;
Butz.style.winExtent.x = 120;
Butz.style.font.size = 16;
// remake the window
Butz.makeWin;

// and add more stuff
Butz.add(\plotTree, { s.plotTree });
Butz.add(\drawMap);
Butz.add(\save_scene);
Butz.add(\chord_explo);

// can access button if needed
Butz.add(\getBut, { |bt| bt.dump });

::

CLASSMETHODS::

private:: initClass, defBounds, blankState

METHOD:: add
add a named action

argument:: name
a Symbol
argument:: action
a function

METHOD:: remove
remove an action  by name
argument:: name
a Symbol

METHOD:: actions
the code::NamedList:: of actions

METHOD:: run
argument:: name
run the action at that name


METHOD:: show
show the Butz window, making it if necessary

METHOD:: makeWin
make the window

METHOD:: numButz
set and get number of buttons to show

METHOD:: style
lookup and set style options for the butz window

METHOD:: w
the butz window

METHOD:: butz
the list of buttons

METHOD:: setButton
argument:: index
set the button at index to show the name stored at that index

METHOD:: updateButtons
update the buttons
